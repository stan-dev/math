# -----------------------------------------------------------------
# Programmer(s): Cody J. Balos @ LLNL
# -----------------------------------------------------------------
# SUNDIALS Copyright Start
# Copyright (c) 2002-2022, Lawrence Livermore National Security
# and Southern Methodist University.
# All rights reserved.
#
# See the top-level LICENSE and NOTICE files for details.
#
# SPDX-License-Identifier: BSD-3-Clause
# SUNDIALS Copyright End
# -----------------------------------------------------------------

# Set the minimum required cmake version
cmake_minimum_required(VERSION @CMAKE_VERSION@)

# Set cache variables for compilers and flags
set(CMAKE_Fortran_COMPILER
  @MPI_Fortran_COMPILER@
  CACHE FILEPATH "Fortran compiler")
set(CMAKE_Fortran_FLAGS
  "@CMAKE_Fortran_FLAGS@"
  CACHE STRING "Fortran compiler flags")

set(CMAKE_C_COMPILER
  @MPI_C_COMPILER@
  CACHE FILEPATH "C compiler")
set(CMAKE_C_FLAGS
  "@CMAKE_C_FLAGS@"
  CACHE STRING "C compiler flags")
if("@CMAKE_C_STANDARD@")
  set(CMAKE_C_STANDARD "@CMAKE_C_STANDARD@"
    CACHE STRING "C standard")
endif()

set(CMAKE_CXX_COMPILER
  @MPI_CXX_COMPILER@
  CACHE FILEPATH "CXX compiler")
set(CMAKE_CXX_FLAGS
  "@CMAKE_CXX_FLAGS@"
  CACHE STRING "CXX compiler flags")
set(CMAKE_CXX_STANDARD
  @CMAKE_CXX_STANDARD@
  CACHE STRING "CXX standard")

set(CMAKE_CUDA_COMPILER
  @CMAKE_CUDA_COMPILER@
  CACHE FILEPATH "CUDA compiler")
set(CMAKE_CUDA_FLAGS
  "@CMAKE_CUDA_FLAGS@"
  CACHE STRING "CUDA compiler flags")
set(CMAKE_CUDA_STANDARD 11)

set(CMAKE_CUDA_HOST_COMPILER
  ${MPI_CXX_COMPILER}
  CACHE FILEPATH "CUDA host compiler")

# Specify project name and languages
project(@SOLVER@_examples @_SUNDIALS_ENABLED_LANGS@)

# Enable testing
include(CTest)

# Require MPI
find_package(MPI REQUIRED)

# ------------------------------------------------------------------------------

# Specify the path to SUNDIALSConfig.cmake
set(SUNDIALS_DIR
  @CMAKE_INSTALL_PREFIX@/@SUNDIALS_INSTALL_CMAKEDIR@
  CACHE PATH "Location of SUNDIALSConfig.cmake")

find_package(SUNDIALS REQUIRED NO_DEFAULT_PATH)

# Set additional libraries
set(SUNDIALS_EXTRA_LIBS @LIBS@ CACHE STRING "Additional libraries")

# Set SUNDIALS targets needed
set(TARGETS_NEEDED @EXAMPLES_CMAKE_TARGETS@)

# List of libraries
set(LIBRARIES_TO_LINK
    ${TARGETS_NEEDED}
    ${SUNDIALS_EXTRA_LIBS})

# ------------------------------------------------------------------------------

# Set the names of the examples to be built and their dependencies
set(examples @EXAMPLES@)
set(examples_dependencies @EXAMPLES_DEPENDENCIES@)
if(examples)
  list(REMOVE_DUPLICATES examples)
endif()

# Create targets for each example
foreach(example ${examples})

  # example source files
  add_executable(${example} ${example}.cpp ${examples_dependencies})

  # directories to include
  target_include_directories(${example} PRIVATE ${SUNDIALS_INCLUDE_DIR})

  # libraries to link against
  target_link_libraries(${example} ${LIBRARIES_TO_LINK})

  # add the example to ctest
  add_test(NAME ${example} COMMAND ${example})

endforeach()
